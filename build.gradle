plugins {
	id 'java'
	id 'org.ajoberstar.git-publish' version '3.0.0'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

dependencies {
	testImplementation("org.assertj:assertj-core:" + project.getProperty('assertjCoreVersion'))
	testImplementation("org.assertj:assertj-joda-time:" + project.getProperty('assertjJodaTimeVersion'))
	testImplementation("org.assertj:assertj-db:" + project.getProperty('assertjDbVersion'))
	testImplementation('junit:junit:4.13.2')

	testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.1")
	testImplementation("org.junit.jupiter:junit-jupiter-params")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

ext {
	docsVersion = project.version.contains('SNAPSHOT') ? 'snapshot' : project.version

	generatedAsciiDocPath = buildDir.toPath().resolve('generated/asciidoc')
}

asciidoctor {

	baseDirFollowsSourceFile()

	sources {
		include '**/index.adoc'
	}

	resources {
		from(sourceDir) {
			include '**/images/**'
			include '**/highlight/**'
			include '**/tocbot-3.0.2/**'
			include '**/*.css'
			include '**/font-awesome/css/*.css'
			include '**/javadoc/**'
			include '**/reports/**'
		}
	}

	attributes 'release-branch': releaseBranch,
			'docs-version': docsVersion,
			'revnumber': version,
			'releaseBranch': version,
			'testDir': '../../../test/java',
			'outdir': outputDir.absolutePath,
			'source-highlighter': 'highlightjs',
			'highlightjsdir': 'highlight',
			'highlightjs-theme': 'railscasts',
			'tabsize': '3',
			'toc': 'left',
			'icons': 'font',
			'sectanchors': true,
			'idprefix': '',
			'idseparator': '-',
			'assertj-core-version': assertjCoreVersion,
			'assertj-guava-version': assertjGuavaVersion,
			'assertj-joda-time-version': assertjJodaTimeVersion,
			'assertj-db-version': assertjDbVersion,
			'stylesheet': '../../../docs/asciidoc/asciidoctor.css',
			'docinfo': 'shared'
}

asciidoctorj {
	modules {
		diagram.use()
	}
}

gitPublish {
	repoUri = 'https://github.com/assertj/doc.git'
	branch = 'gh-pages'
	contents {
		from file(asciidoctor.outputDir.path + '/user-guide')
	}
}

test {
	useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
	}

	reports {
		html.required = true
	}
}

repositories {
	mavenCentral()
}
