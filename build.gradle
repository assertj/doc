plugins {
	id 'java'
	id 'org.ajoberstar.git-publish' version '2.1.3'
	id 'org.asciidoctor.jvm.convert' version '3.1.0'
}

dependencies {
	testCompile("org.assertj:assertj-core:" + project.getProperty('assertjCoreVersion'))
	testCompile("org.assertj:assertj-db:" + project.getProperty('assertjDbVersion'))
	testCompile('junit:junit:4.13')

	def junitJupiterVersion = '5.6.1'
	testCompile("org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion")
	testCompile("org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion")
	testRuntime("org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion")
}

ext {
	docsVersion = project.version.contains('SNAPSHOT') ? 'snapshot' : project.version

	generatedAsciiDocPath = buildDir.toPath().resolve('generated/asciidoc')
}

asciidoctor {

	baseDirFollowsSourceFile()

	sources {
		include '**/index.adoc'
	}

	resources {
		from(sourceDir) {
			include '**/images/**'
			include '**/highlight/**'
			include '**/tocbot-3.0.2/**'
			include '**/*.css'
			include '**/font-awesome/css/*.css'
			include '**/javadoc/**'
		}
	}

	attributes 'release-branch': releaseBranch,
			'docs-version': docsVersion,
			'revnumber': version,
			'releaseBranch': version,
			'testDir': '../../../test/java',
			'outdir': outputDir.absolutePath,
			'source-highlighter': 'highlightjs',
			'highlightjsdir': 'highlight',
			'highlightjs-theme': 'railscasts',
			'tabsize': '3',
			'toc': 'left',
			'icons': 'font',
			'sectanchors': true,
			'idprefix': '',
			'idseparator': '-',
			'assertj-core-version': assertjCoreVersion,
			'assertj-db-version': assertjDbVersion,
			'stylesheet': '../../../docs/asciidoc/asciidoctor.css',
			'docinfo': 'shared'
}

asciidoctorj {
	modules {
		diagram.use()
	}
}

gitPublish {
	repoUri = 'https://github.com/assertj/doc.git'
	branch = 'gh-pages'
	contents {
		from file(asciidoctor.outputDir.path + '/user-guide')
	}
}

test {
	useJUnitPlatform()

	testLogging {
		events "passed", "skipped", "failed"
	}

	reports {
		html.enabled = true
	}
}

repositories {
	mavenCentral()
}
